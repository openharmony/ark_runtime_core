# Copyright (c) 2021-2022 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//ark/runtime_core/ark_config.gni")
import("//build/ohos.gni")

config("arkziparchive_config") {
  include_dirs = [
    "$ark_root/libziparchive",
    "$ark_root/libpandabase",
  ]

  if (!ark_standalone_build) {
    include_dirs += [
      "//third_party/zlib",
      "//third_party/zlib/contrib/minizip",
    ]
  } else {
    include_dirs += [ "$ark_third_party_root/miniz" ]
  }
}

ohos_shared_library("libarkziparchive") {
  sources = [ "zip_archive.cpp" ]

  configs = [
    ":arkziparchive_config",
    "$ark_root:ark_config",
    sdk_libc_secshared_config,
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase",
    sdk_libc_secshared_dep,
  ]

  if (!ark_standalone_build) {
    deps += [ "//third_party/zlib:libz" ]
  } else {
    deps += [ "$ark_third_party_root/miniz:libminiz" ]
  }
  if (!is_standard_system) {
    relative_install_dir = "ark"
  }
  output_extension = "so"
  if (is_mingw) {
    output_extension = "dll"
  }
  subsystem_name = "ark"
  part_name = "ark"
}

ohos_static_library("libarkziparchive_frontend_static") {
  sources = [ "zip_archive.cpp" ]

  configs = [
    ":arkziparchive_config",
    "$ark_root:ark_config",
    sdk_libc_secshared_config,
  ]

  deps = [
    "$ark_root/libpandabase:libarkbase_frontend_static",
    sdk_libc_secshared_dep,
  ]

  if (!ark_standalone_build) {
    deps += [ "//third_party/zlib:libz" ]
  } else {
    deps += [ "$ark_third_party_root/miniz:libminiz" ]
  }

  subsystem_name = "ark"
  part_name = "ark"
}
